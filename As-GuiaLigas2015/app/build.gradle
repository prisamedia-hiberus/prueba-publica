def keyStoreProperties = 'sign/keystore.properties'

def getVersionCodeAsLiga2015 = { ->
    def code = project.hasProperty('versionCodeAsLiga2015') ? versionCodeAsLiga2015.toInteger() : -1
    return code
}

def getVersionNameAsLiga2015 = { ->
    def name = project.hasProperty('versionNameAsLiga2015') ? versionNameAsLiga2015.toString() : ''
    return name
}

def getVersionCodeCopaAmerica2016 = { ->
    def code = project.hasProperty('versionCodeCopaAmerica2016') ? versionCodeCopaAmerica2016.toInteger() : -1
    return code
}

def getVersionNameCopaAmerica2016 = { ->
    def name = project.hasProperty('versionNameCopaAmerica2016') ? versionNameCopaAmerica2016.toString() : ''
    return name
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 17
    }



    File singFile = getRootProject().file(keyStoreProperties)
    if (singFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(singFile))

        signingConfigs {
            copaamerica2016 {
                storeFile rootProject.file(properties['keystorePathCopaAmerica'])
                storePassword properties['storePasswordCopaAmerica']
                keyAlias properties['keyAliasCopaAmerica']
                keyPassword properties['keyPasswordCopaAmerica']
            }

            asguialiga2015 {
                storeFile rootProject.file(properties['keystorePathAsGuiaLiga'])
                storePassword properties['storePasswordAsGuiaLiga']
                keyAlias properties['keyAliasAsGuiaLiga']
                keyPassword properties['keyPasswordAsGuiaLiga']
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

        }
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    productFlavors {


        asguialiga2015{
            applicationId "com.diarioas.guialigas"
            resValue "string", "authority", applicationId
            buildConfigField "String", "URL_REMOTE_CONFIG_FILE", "\"http://as.com/apps/guia_de_la_liga/config/v2.x/v2.1/conf-guia.xml\""
            buildConfigField "String", "TEAM_IMAGE_PREFIX", "\"escudo_\""
            buildConfigField "String", "AD_KEY", "\"/7811748/as_guialiga_app/android/\""
            buildConfigField "boolean", "SINGLE_COMPETITION", "false"
            if(getVersionCodeAsLiga2015().equals(-1)){
                versionCode = 6
            } else {
                versionCode = getVersionCodeAsLiga2015()
            }

            if(getVersionNameAsLiga2015().equals('')){
                versionName = "1.0.6"
            } else {
                versionName = getVersionNameAsLiga2015()
            }
            signingConfig signingConfigs.asguialiga2015

        }
        copaamerica2016{
            applicationId "com.diarioas.copaamerica"
            resValue "string", "authority", applicationId
            buildConfigField "String", "URL_REMOTE_CONFIG_FILE", "\"http://as.com/apps/guia_copa_america/config/v1.x/v1.1/conf-guia.xml\""
            buildConfigField "String", "TEAM_IMAGE_PREFIX", "\"flag_\""
            buildConfigField "String", "AD_KEY", "\"/7811748/as_copa_america_app/android/\""
            buildConfigField "boolean", "SINGLE_COMPETITION", "true"
            if(getVersionCodeCopaAmerica2016().equals(-1)){
                versionCode = 1
            } else {
                versionCode = getVersionCodeCopaAmerica2016()
            }

            if(getVersionNameCopaAmerica2016().equals('')){
                versionName = "1.0.0"
            } else {
                versionName = getVersionNameCopaAmerica2016()
            }
            signingConfig signingConfigs.copaamerica2016

        }
    }

}



repositories {
    jcenter()

    maven {
        url  "http://dl.bintray.com/urbanairship/android"
    }
    flatDir {
        dirs 'aar'
    }
}

dependencies {
    compile 'com.urbanairship.android:urbanairship-sdk:6.1.+'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'
    compile 'com.google.android.gms:play-services-location:7.5.0'
    compile 'com.google.android.gms:play-services-ads:7.5.0'
    compile(name: 'lib-ads-android-gradle-release', ext: 'aar')
    compile files('libs/admsAppLibrary.jar')
    compile files('libs/comscore.jar')
    compile files('libs/picasso-2.5.2.jar')
    compile files('libs/xmlwise-1_2.jar')
}
